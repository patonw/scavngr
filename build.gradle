plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'

group = 'net.varionic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    resilience4jVersion = "0.17.0"
    mapstructVersion = "1.3.0.Final"
    swaggerVersion = "2.9.2"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

sourceSets {
    integration {
        java.srcDir "src/integration/java"
        resources.srcDir "src/integration/resources"

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.shouldRunAfter test

repositories {
    mavenCentral()
}

dependencies {
    compile "org.mapstruct:mapstruct:${mapstructVersion}"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    implementation "io.github.resilience4j:resilience4j-circuitbreaker:${resilience4jVersion}"
    implementation "io.vavr:vavr:0.9.3"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.h2database:h2"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

    integrationAnnotationProcessor 'org.projectlombok:lombok'
    integrationAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    integrationCompileOnly 'org.projectlombok:lombok'
    integrationRuntimeOnly 'org.postgresql:postgresql'
    integrationImplementation "org.testcontainers:testcontainers:1.11.3"
    integrationImplementation "org.testcontainers:postgresql:1.11.3"
}
